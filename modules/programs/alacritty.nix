{
  pkgs,
  lib,
  config,
  inputs,
  ...
}: let
  cfg = config.modules.alacritty;
  # base16-alacritty provides the mustache template for
  # the color scheme
  base16-alacritty = "${inputs.base16-alacritty}/templates/default-256.mustache";
in {
  options = {
    modules.alacritty = {
      enable =
        lib.mkEnableOption "whether to enable the alacritty configuration";
    };
  };

  config = lib.mkIf cfg.enable {
    programs.alacritty = {
      enable = true;
      package = pkgs.alacritty-graphics;
      settings = {
        font.size = config.utils.fonts.active.monospace.size;
        general.import = let
          themeFile = pkgs.writeTextFile {
            name = "base16.toml";
            text = config.utils.mustache.eval-base16 (builtins.readFile base16-alacritty);
          };
        in [themeFile];
        window = {
          padding = {
            x = 6;
            y = 6;
          };
          dimensions = {
            columns = 55;
            lines = 17;
          };
        };
        cursor.style.shape = "Block";
        terminal.shell = {
          program = "zsh";
          args = [
            "-c"
            "fastfetch && exec zsh"
          ];
        };
      };
    };
  };
}
